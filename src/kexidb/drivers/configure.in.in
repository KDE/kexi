dnl ================
dnl checks for MySQL
dnl taken form KDEDB
dnl ================

AC_ARG_ENABLE(mysql, [  --enable-mysql              build MySQL-plugin [default=yes]],
              mysql_plugin=$enableval, mysql_plugin=yes)

AC_ARG_WITH(mysql_includes,
[  --with-mysql-includes=DIR   use MySQL-includes installed in this directory ],
[
  ac_mysql_incdir=$withval
], ac_mysql_incdir=/usr/local/include
)

AC_ARG_WITH(mysql_libraries,
[  --with-mysql-libraries=DIR  use MySQL-libs installed in this directory ],
[
  ac_mysql_libdir=$withval
], ac_mysql_libdir=/usr/local/lib
)

dnl check for MySQL should be compiled
dnl and where headers and libraries are installed
dnl if present compile mysql-plugin

AC_MSG_CHECKING([for MySQL])

compile_mysql_plugin="yes"

if test "x$mysql_plugin" != "xyes"; then
  compile_mysql_plugin="no"
fi

KDE_FIND_PATH(mysql_config, MYSQL_CONFIG, [${prefix}/bin ${exec_prefix}/bin /usr/local/bin /opt/local/b\
in /usr/bin /opt/mysql/bin /usr/local/mysql/bin ], [
  AC_MSG_WARN([Could not find MySQL anywhere, check http://www.mysql.com/])
])

if test -n "$MYSQL_CONFIG"; then
	mysql_incdir=`$MYSQL_CONFIG --cflags| $SED -e "s,mysql'$,,g"| $SED -e "s,-I',,g"`
	mysql_libdir=`$MYSQL_CONFIG --libs| $SED -e "s,',,g"`
	MYSQL_INC=$mysql_incdir
	MYSQL_LIBS=$mysql_libdir
	AC_SUBST(MYSQL_INC)
	AC_SUBST(MYSQL_LIBS)
else
	compile_mysql_plugin="no"
fi

if test "$compile_mysql_plugin" = "yes"; then
  AC_MSG_RESULT([libraries $mysql_libdir, headers $mysql_incdir])
else
  AC_MSG_RESULT(not found)
fi

AM_CONDITIONAL(compile_mysql_plugin, test "$compile_mysql_plugin" = "yes")

dnl =====================
dnl checks for PostgreSQL
dnl =====================

dnl add configure-args

AC_ARG_ENABLE(pgsql, [  --enable-pgsql              build PostgreSQL-plugin [default=yes] ],
              pgsql_plugin=$enableval, pgsql_plugin=yes)

AC_ARG_WITH(pgsqlincdir,
[  --with-pgsql-includes=DIR   use PostgreSQL-includes installed in this directory ],
[
  ac_pgsql_dir=$withval
], ac_pgsql_dir=/usr/local/include
)

AC_ARG_WITH(pgsqllibdir,
[  --with-pgsql-libraries=DIR  use PostgreSQL-libraries installed in this directory ],
[
  ac_pgsql_libdir=$withval
], ac_pgsql_libdir=/usr/local/lib
)

dnl check for PostgreSQL should be compiled
dnl and where headers and libraries are installed
dnl if present compile postgresql-plugin

AC_MSG_CHECKING([for PostgreSQL])

compile_pgsql_plugin="yes"

if test "x$pgsql_plugin" != "xyes"; then
  compile_pgsql_plugin="no"
fi

if test "$compile_pgsql_plugin" = "yes"; then
  dnl check header files
  pgsql_incdirs="$ac_pgsql_incdir /usr/include /usr/include/pgsql /usr/include/postgresql /usr/local/include /usr/local/pgsql/include /opt/postgres/include"
  AC_FIND_FILE(libpq-fe.h, $pgsql_incdirs, pgsql_incdir)
  if test ! -r $pgsql_incdir/libpq-fe.h; then
    compile_pgsql_plugin="no"
    AC_MSG_CHECKING("include ")
  else
    PG_INCDIR=$pgsql_incdir
    AC_SUBST(PG_INCDIR)
  fi
fi

if test "$compile_pgsql_plugin" = "yes"; then
  dnl check libraries
  pgsql_libdirs="$ac_pgsql_libdir /usr/lib /usr/local/lib /usr/local/pgsql/lib /opt/postgres/lib"
  AC_FIND_FILE(libpq.so, $pgsql_libdirs, pgsql_libdir)
  if test ! -r $pgsql_libdir/libpq.so; then
    compile_pgsql_plugin="no"
    AC_MSG_CHECKING("library ")
  else
    PG_LIBDIR=$pgsql_libdir
    AC_SUBST(PG_LIBDIR)
  fi
fi

if test "$compile_pgsql_plugin" = "yes"; then
  AC_MSG_RESULT([libraries $pgsql_libdir, headers $pgsql_incdir])
else
  AC_MSG_RESULT(not found)
fi

AM_CONDITIONAL(compile_pgsql_plugin, test "$compile_pgsql_plugin" = "yes")
